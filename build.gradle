import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.kotlin.allOpenPlugin
		classpath libs.kotlinx.benchmark.gradlePlugin
		classpath libs.dokka.gradlePlugin
		classpath libs.gradleMavenPublishPlugin
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka'

group = project.property("GROUP") as String
version = project.property("VERSION_NAME") as String

kotlin {
	explicitApi()

	androidNativeArm32()
	androidNativeArm64()
	androidNativeX64()
	androidNativeX86()

	iosArm64()
	iosSimulatorArm64()
	iosX64()

	linuxArm32Hfp()
	linuxArm64()
	linuxX64()

	macosArm64()
	macosX64()

	mingwX64()

	js {
		nodejs()
	}

	jvm()

	tvosArm64()
	tvosSimulatorArm64()
	tvosX64()

	wasmJs().nodejs()

	watchosArm32()
	watchosArm64()
	watchosDeviceArm64()
	watchosSimulatorArm64()
	watchosX64()

	sourceSets {
		commonTest {
			dependencies {
				implementation libs.kotlin.test
				implementation libs.assertk
			}
		}

		// Implementations using Kotlin stdlib types or the JVM to which the stdlib aliases.
		unspecializedMain {
			dependsOn(commonMain)
		}
		// Implementations which typealias to platform-specific representations.
		specializedMain {
			dependsOn(commonMain)
		}

		// Implementations using Foundation.
		darwinMain {
			dependsOn(specializedMain)
		}
		darwinTest {
			dependsOn(commonTest)
		}

		jsMain {
			dependsOn(specializedMain)
		}
		jvmMain {
			dependsOn(unspecializedMain)
		}
	}

	targets.each { target ->
		if (target.platformType == KotlinPlatformType.native || target.platformType == KotlinPlatformType.wasm) {
			if (target.name.contains('os')) {
				target.compilations.main.defaultSourceSet.dependsOn(sourceSets.darwinMain)
				target.compilations.test.defaultSourceSet.dependsOn(sourceSets.darwinTest)
			} else {
				target.compilations.main.defaultSourceSet.dependsOn(sourceSets.unspecializedMain)
			}
		}
	}
}

plugins.withType(NodeJsRootPlugin).configureEach {
	kotlinNodeJs {
		nodeVersion = '20.0.0'
	}
}

tasks.withType(KotlinJvmCompile).configureEach {
	compilerOptions.jvmTarget = JvmTarget.JVM_1_8
}

tasks.withType(JavaCompile).configureEach {
	sourceCompatibility = JavaVersion.VERSION_1_8.toString()
	targetCompatibility = JavaVersion.VERSION_1_8.toString()
}

tasks.withType(KotlinCompilationTask).configureEach {
	compilerOptions {
		// > Task :compileTestKotlinWasmJs FAILED
		// w: duplicate library name: kotlin
		allWarningsAsErrors = !name.containsIgnoreCase("wasm")
		freeCompilerArgs.add('-Xexpect-actual-classes')
	}
}
